#!/bin/sh
#| -*- scheme -*- |#
:; exec gosh -- $0 "$@"

;; Copyright (C) 2010 Masatake YAMATO

;; This library is free software: you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation, either version 3 of the License, or
;; (at your option) any later version.

;; This library is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with this library.  If not, see <http://www.gnu.org/licenses/>.
;;
;; Author: Masatake YAMATO <yamato@redhat.com>
;;
(use srfi-1)
(use srfi-11)
(use srfi-13)

(use gauche.sequence)
(use gauche.parseopt)
(use gauche.process)
(use gauche.threads)
(use gauche.selector)

(use file.util)
(use util.queue)

(use es.src.syslog)
(use es.dest.syslog)

(debug-print-width #f)


(define perg-cmd "perg")

(define (package-version->xgettxt pkg ver dist)
  (let1 c (substring pkg 0 1)
    (any file-is-readable? 
	 (values (format "/srv/sources/dists/~a/plugins/cache/~a-xgettext.es" dist pkg)
		 #t)
	 (values (format "/srv/sources/sources/~a/~a/~a/plugins/xgettext/xgettext.es"
			 c pkg ver)
		 #t)
	 (values (format "/srv/sources/sources/~a/~a/~a/plugins/xgettext/xgettext.out"
			 c pkg ver)
		 #f))))
	 
(define fat-guys (list
		  #/^kernel.*/ 
		  "glibc"
		  "bash"
		  "initscripts"
		  "mkinitrd"
		  ))


(define basename-laundry-table (hash-table 'equal? 
					   '("mountd" . "rpc.mountd")))

(define (basename-laundry basename)
  (or (ref basename-laundry-table basename #f)
      (let1 m (#/^([^(]+)\([^)]+\)/ basename)
	(if m
	    (m 1)
	    #f))
      basename))

(define (basename->srpm-paths basename dist libexec-dir)
  (let* ((basename   (basename-laundry basename))
	 (file->rpms (build-path libexec-dir "list-rpmnames-by-file.sh"))
	 (rpm->rpms  (build-path libexec-dir "list-requires-by-package-name.sh"))
	 (rpm->srpms (build-path libexec-dir "list-srpmnames-by-package-name.sh"))
	 (db (format "/srv/sources/dists/~a/plugins/rpms2sqldb/rhel-i386-~d.sqlite" 
		     dist
		     ((#/rhel([0-9]).*/ dist) 1)
		     ))
	 (fat-guys? (lambda (pkg) (any identity (map 
						 (lambda (test)
						   (cond
						    ((string? test) (equal? test pkg))
						    ((is-a? test <regexp>) (test pkg))
						    (else #f)))
						 fat-guys)))))
    (let1 rpms 
	(cond
	 ;; kernel-xen???
	 ((#/^kernel.*/ basename) (list basename))
	 (else
	  (let loop ((input-rpms (process-output->string-list `(,file->rpms ,db ,basename)))
		      (output-rpms (list)))
	     (if (null? input-rpms)
		 output-rpms
		 (let1 temp-rpms (process-output->string-list `(,rpm->rpms ,db ,(car input-rpms)))
		   (apply loop (let merge ((input-rpms (cdr input-rpms))
					   (output-rpms (if (member (car input-rpms) output-rpms)
							    output-rpms
							    (cons (car input-rpms) output-rpms)))
					   (temp-rpms temp-rpms))
				 (if (null? temp-rpms)
				     (list input-rpms output-rpms)
				     (let1 new-rpm (car temp-rpms)
				       (let1 in-output? (or (member new-rpm output-rpms)
							    (fat-guys? new-rpm))
					 (merge 
					  ;; If too slow, use this:
					  ;; input-rpms 
					  (if in-output? input-rpms (cons new-rpm input-rpms))
					  (if in-output? output-rpms (cons new-rpm output-rpms))
					  (cdr temp-rpms))))))))))))
      
      (let1 srpms (apply lset-union equal? (map 
					    (lambda (rpm) 
					      (process-output->string-list `(,rpm->srpms ,db ,rpm)))
					    rpms))
	(if (null? srpms)
	    (list)
	    (let loop ((srpms srpms)
		       (result (list)))
	      (if (null? srpms)
		  result
		  (let1 srpm (car srpms)
		    (let1 verdir (guard (e (else #f))
					(sys-readlink (format "/srv/sources/dists/~a/packages/~a/~a"
							      dist
							      (substring srpm 0 1)
							      srpm)))
		      (if verdir
			  (loop (cdr srpms)  (cons (list srpm (sys-basename verdir))
						   result))
			  (loop (cdr srpms)  result)))))))))))

(define kget
  (case-lambda
   ((klist key default)
    (cond
     ((null? klist) default)
     ((eq? (car klist) key) (cadr klist))
     (else (kget (cdr klist) key default))))
   ((klist key)
    (kget klist key #f))))

(define (kget-default$ default-klist)
  (lambda (klist key)
    (kget klist key (kget default-klist key))))

;; TODO: khave?
;; TODO: multiple
(define (kattach klist key value)
  ;; fold?
  (reverse (cons value (cons key (reverse klist)))))

(define (kreplace klist key value)
  (reverse (let loop ((input klist)
		      (result (list)))
	     (cond
	      ((null? input) result)
	      ((eq? (car input) key)
	       (loop (if (null? (cdr input))
			 (list)
			 (cddr input))
		     (cons value (cons key result)))
	       )
	      (else
	       (loop (cdr input) (cons (car input) result)))))))

(define (kupdate klist key proc)
  (reverse (let loop ((input klist)
		      (result (list)))
	     (cond
	      ((null? input) result)
	      ((eq? (car input) key)
	       (loop (if (null? (cdr input))
			 (list)
			 (cddr input))
		     (cons (proc (cadr input)) (cons key result)))
	       )
	      (else
	       (loop (cdr input) (cons (car input) result)))))))


(define (kdrop klist key)
  (reverse (let loop ((input klist)
		      (result (list)))
	     (cond
	      ((null? input) result)
	      ((eq? (car input) key)
	       (if (null? (cdr input))
		   ;; broken klist
		   result
		   (loop (cddr input) result)))
	      (else
	       (loop (cdr input) (cons (car input) result)))))))

(define kget&drop
  (case-lambda
   ((klist key default)
    (let loop ((klist        klist)
	       (result-klist (list))
	       (result-value default))
      (if (null? klist)
	  (values result-value (reverse! result-klist))
	  (if (eq? key (car klist))
	      (loop (cddr klist)
		    result-klist
		    (cadr klist))
	      (loop (cdr klist)
		    (cons (car klist) result-klist)
		    result-value)))))
   ((klist key)
    (kget&drop klist key #f))))

;; gosh> (ktranspose '(foo :a (bar 1 2 3) :b (fox "a" "b" "c")) :b)
;; (fox "a" "b" "c" :b! (foo :a (bar 1 2 3)))
;; gosh > (ktranspose '(fox "a" "b" "c" :b! (foo :a (bar 1 2 3))) :b!)
;; (foo :a (bar 1 2 3) :b (fox "a" "b" "c"))
;; gosh> (ktranspose '(foo :a (bar 1 2 3) :b (fox "a" "b" "c")) :b :enter)
;; (fox "a" "b" "c" :enter (foo :a (bar 1 2 3)))
;; gosh> (ktranspose (ktranspose '(foo :a (bar 1 2 3) :b (fox "a" "b" "c")) :b :enter) :enter :b)
;; (foo :a (bar 1 2 3) :b (fox "a" "b" "c"))
;; gosh> 
(define ktranspose 
  (case-lambda
   ((klist key-pull key-push)
    (receive (new old) (kget&drop klist key-pull)
      (kattach new key-push old)))
   ((klist key-pull)
    (ktranspose klist 
		key-pull
		(make-keyword (let1 key-pull-string (x->string key-pull)
				(rxmatch-if (#/(.*)!$/ key-pull-string)
				    (#f body)
				  body
				  (string-append key-pull-string "!"))))))))

(define-class <director> ()
  ((dist :init-keyword :dist)
   (max-groups :init-keyword :max-groups)
   (basename->srpm-paths :init-keyword :basename->srpm-paths)
   (basenames :init-form (make-hash-table 'equal?))
   (packages  :init-form (make-hash-table 'equal?))
   (logs      :init-form (make-queue))
   ))

(define-class <basename> ()
  ((packages :init-keyword :packages)
   (uniq-logs :init-form (make-hash-table 'equal?))
   ))

(define-class <package> ()
  ((dist :init-keyword :dist)
   (pkgname :init-keyword :pkgname)
   (version :init-keyword :version)
   (to-perg-queue :init-form (make-queue))
   (pergs :init-form (list))
   (to-pergs-ports :init-form (list))
   (from-pergs-ports :init-form (list))
   ))

(define-method boot ((package <package>))
  (let-values (((xgettext pre-processed?)  (package-version->xgettxt
					    (ref package 'pkgname)
					    (ref package 'version)
					    (ref package 'dist))))
    ;; FIXME make parametric
    (let1 limit 20
      (when xgettext
	(let ((limit-flag (format "--limit=~d" limit)))
	  (let1 proc (run-process `(,perg-cmd "--format=es" 
					      ,limit-flag
					      ,(format "--pre-processed=~a"
						       (if pre-processed? "yes" "no"))
					      =
					      ,xgettext) 
				  :input :pipe
				  :output :pipe)
	    (push! (ref package 'pergs) proc)
	    (push! (ref package 'to-pergs-ports) (process-input proc))
	    (push! (ref package 'from-pergs-ports) (process-output proc))))))))
		

(define-method make-package ((director <director>) 
			     (pkgname <string>)
			     (version <string>))
  (let1 pkg (make <package> 
	      :dist (ref director 'dist)
	      :pkgname pkgname
	      :version version)
    ;; FIXME: This needs guard.
    (boot pkg)
    (set! (ref (ref director 'packages) pkgname) pkg)
    pkg))

(define-method packages-for-basename ((director <director>) 
				      (basename <string>))
  (let loop ((candidates ((ref director 'basename->srpm-paths) basename))
	     (exists (ref director 'packages))
	     (result (list)))
    (if (null? candidates)
	result
	(let* ((c (car candidates))
	       (pkg (or (ref exists (car c) #f)
			(make-package director (car c) (cadr c)))))
	  (loop (cdr candidates) exists (cons pkg result))))))

(define-method make-basename ((director <director>)
			      (basename <string>))
  (let1 b (make <basename> 
	    :packages (packages-for-basename director basename)
	    )
    (set! (ref (ref director 'basenames)  basename)  b)
    b))

(define-method prepare ((package  <package>)
			syslog)
  (enqueue! (ref package 'to-perg-queue) syslog))

(define-method prepare ((basename <basename>)
			syslog)
  (let* ((log-line (kget syslog :msg))
	 (uniq-logs (ref basename 'uniq-logs)))
    (unless (ref uniq-logs log-line #f)
      (set! (ref uniq-logs log-line) 
	    (make-tree-map (lambda (k1 k2)
			     (and (eqv? (car k1) (car k2))
				  (equal? (cadr k1) (cadr k2))))
			   (lambda (k1 k2)
			     (and (>= (car k1) (car k2))
				  ;(string< (cadr k1) (cadr k2))
				  ))
			     ))
      (for-each
       (cute prepare <> syslog)
       (ref basename 'packages)))))

(define-method prepare ((director <director>)
			log-line)
  (let1 syslog (es<-syslog log-line)
    (enqueue! (ref director 'logs) syslog)
    (unless (memq :raw syslog)
      (let1 basename (kget syslog :cmd)
	(when basename
	  (let1 b (or (ref (ref director 'basenames) basename #f)
		      (make-basename director basename))
	    (prepare b syslog)))))
    director))

(define (writeln r oport)
  (write r oport)
  (newline oport))

;; FIXME: Take :match-chars into account.
(define (take-group filelines limit)
  (if (null? filelines)
      filelines
      (let1 get-msg (lambda (xgettext) (kget xgettext :msg))
	(let loop ((key          (get-msg (car filelines)))
		   (candidate    (car filelines))
		   (rest         (cdr filelines))
		   (result       (list))
		   (n            0))
	  (cond
	   ((eq? n limit)
	    (reverse result))
	   ((equal? (get-msg candidate) key)
	    (if (null? rest)
		(reverse (cons candidate result))
		(loop key (car rest) (cdr rest) (cons candidate result) n)))
	   (else
	    (loop (get-msg candidate) candidate rest result (+ n 1))))))))

(define-method resolve ((basename <basename>)
			(uniq-log <string>)
			filelines
			max-groups
			pkgname
			version)
  ;; Pick higher rank upto MAX-GROUPS.
  ;; Put them to the tree-map.
  (let ((filelines (take-group filelines max-groups))
	(extra     (list :package pkgname :version version))
	(tree-map  (ref (ref basename 'uniq-logs) uniq-log)))
    (for-each
     (lambda (fileline)
       (let* ((match-chars (kget fileline :match-chars))
	      (msg (kget fileline :msg))
	      (key (list match-chars msg)))
	 (tree-map-push! tree-map key 
			 (append! fileline extra))
	 ))
     filelines)))


(define-method resolve ((director <director>))
  (let ((output-selector (make <selector>))
	(input-selector  (make <selector>))
	(n-output-ports 0)
	(n-input-ports 0))
    (hash-table-for-each
     (ref director 'packages)
     (lambda (k package)
       (for-each
	(lambda (to-perg-port)
	  (set! n-output-ports (+ n-output-ports 1))
	  (selector-add! output-selector to-perg-port
			 (lambda (output-port flag)
			   (let* ((queue (ref package 'to-perg-queue))
				  (syslog (dequeue! queue)))
			     (when syslog
			       (guard (e
				       (else (dequeue-all! queue)))
				      (writeln `(log ,(kget syslog :msg)
						     :basename ,(kget syslog :cmd)
						     ;; :pkgname and :version can
						     ;; be attached here
						     )
					       output-port)))
			     (when (queue-empty? queue)
			       (selector-delete! output-selector output-port #f #f)
			       (close-output-port output-port)
			       (set! n-output-ports (- n-output-ports 1))
			       )))
			 '(w)))
	(ref package 'to-pergs-ports))
       (for-each
	(lambda (from-perg-port)
	  (set! n-input-ports (+ n-input-ports 1))
	  (selector-add! input-selector from-perg-port
			 (lambda (input-port flag)
			   (let1 log+filelines (read input-port)
			     (if (eof-object? log+filelines)
				 (begin (selector-delete! input-selector input-port #f #f)
					(close-input-port input-port)
					(set! n-input-ports (- n-input-ports 1)))
				 (resolve (ref (ref director 'basenames) 
					       (kget log+filelines :basename))
					  (cadr log+filelines)
					  (kget log+filelines :filelines)
					  (ref director 'max-groups)
					  (ref package 'pkgname)
					  (ref package 'version)
					  ))))

			 '(r)))
	(ref package 'from-pergs-ports))
       ))
    ;;
    (let ((output-thread (make-thread (rec (loop)
					   (when (< 0 n-output-ports)
					     (let1 status (selector-select output-selector)
					       (unless (eq? status 0)
						 (loop)))))
				      "output-thread"))
	  (input-thread (make-thread (rec (loop)
					  (when (< 0 n-input-ports)
					    (let1 status (selector-select input-selector)
					      (unless (eq? status 0)
						(loop)))))
				     "input-thread")))
      (thread-start! output-thread)
      (thread-start! input-thread)
      (thread-join! output-thread)
      (thread-join! input-thread))
    ;;
    (hash-table-for-each
     (ref director 'packages)
     (lambda (k package)
       (for-each 
	process-wait
	(ref package 'pergs)))))
  director)

(define-method report-raw ((director <director>)
			   rawlog)
  #|(format #t "[_] ~a\n" (kget rawlog :raw))|#
  (writeln rawlog (current-output-port))
  )
(define-method report-syslog ((director <director>)
			      syslog
			      fileliens)
  (writeln (kattach syslog :filelines fileliens)
	   (current-output-port))
  #|(format #t "[+] ~a\n" (kget syslog :msg))
  (for-each
   (lambda (max-chars_msg-filelines)
     (let* ((h (car max-chars_msg-filelines))
	    (t (cdr max-chars_msg-filelines)))
       (format #t "	<~d>~s\n" (car h) (cadr h))
       (for-each 
	(lambda (fileline)
	  (format #t "		~a,~a,~a,~s\n" 
		  (kget fileline :package)
		  (kget fileline :version)
		  (kget fileline :file)
		  (kget fileline :line))
	  )
	t)
       ))
   fileliens)
  |#)



(define (dequeue-safe! queue . default)
  (let1 default (if (null? default) #f  default)
    (if (queue-empty? queue)
	default
	(dequeue! queue))))

(define (take-higher-groups tree-map max-groups)
  (let1 alist (tree-map->alist tree-map)
    (if (null? alist)
	alist
	(let1 max-match-chars (car (car (car alist)))
	  (let loop ((queue (make-queue))
		     (alist alist)
		     (max-groups max-groups)
		     (max-match-chars max-match-chars))
	    (if (null? alist)
		(dequeue-all! queue)
		(let* ((elt (car alist))
		       (match-chars (car (car elt))))
		  (if (> max-match-chars match-chars)
		      (let1 next-groups (- max-groups 1)
			(if (< 0 next-groups)
			    (begin
			      (enqueue! queue elt)
			      (loop queue (cdr alist) next-groups match-chars))
			    (dequeue-all! queue)))
		      (begin 
			(enqueue! queue elt)
			(loop queue (cdr alist) max-groups max-match-chars))))))))))

(define-method report ((director <director>))
  (let ((queue (ref director 'logs))
	(max-groups (ref director 'max-groups)))
    (let loop ((syslog (dequeue-safe! queue)))
      (when syslog
	(cond
	 ((or (memq :raw syslog) (not (kget syslog :cmd)))
	  (report-raw director syslog))
	 (else
	  (let1 tree-map (ref 
			  (ref 
			   (ref 
			    (ref director 'basenames)
			    (kget syslog :cmd))
			   'uniq-logs)
			  (kget syslog :msg))
	    (report-syslog director 
			   syslog
			   (take-higher-groups 
			    tree-map
			    max-groups
			    )))))
	(loop (dequeue-safe! queue))))))

(define (show-help prog n)
  (format #t "~a --help\n" prog)
  (format #t "~a [--libexec-dir=LIBEXECDIR(=~s)] [--max-groups=N] [--dist=DIST] < /var/log/message\n" 
	  prog
	  "@prefix@/libexec/perg")
  (exit n))

(define (read-line-safe . port)
  (let1 port (if (null? port)
		 (current-input-port)
		 (car port))
    (guard (e
	    (else #f))
	   (read-line port))))

(define (main args)
  (let-args (cdr args)
      ((help "h|help" => (cute show-help (car args) 0))
       (dist "dist=s" "rhel5su4")
       (max-groups "max-groups=i" 10)
       (libexec-dir "libexec-dir=s" "@prefix@/libexec/perg")
       )
    (report (resolve (let loop ((l (read-line-safe))
				(director (make <director>
					    :dist dist
					    :max-groups max-groups
					    :basename->srpm-paths (cute basename->srpm-paths
								   <> 
								   dist
								   libexec-dir
								   ))))
		       (if (eof-object? l)
			   director
			   (loop (read-line-safe) 
				 (if l
				     (prepare director l)
				     director))))))))
