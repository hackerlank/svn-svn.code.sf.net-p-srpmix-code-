#!/bin/sh
#| -*- scheme -*- |#
:; exec gosh -- $0 "$@"

(use srfi-1)
(use srfi-13)
(use util.match)
(use util.list)
(use gauche.process)
(use gauche.parameter)
(use file.util)
(use gauche.parseopt)
(use gauche.sequence)

(debug-print-width #f)

(define (kref es k)
  (cadr (or (memq k es)
	    (list k #f))))


(define blacklist 
  (list
   #/^__dso_handle.*/			;???
   #/^__FUNCTION__\..*/
   #/^__PRETTY_FUNCTION__\..*/
   #/^__libc_csu_init$/
   #/^__do_global_ctors_aux$/
   #/^_start$/
   #/^call_gmon_start$/
   #/^__do_global_dtors_aux$/
   #/^frame_dummy$/
   #/^_start$/
   ))

(define tables (make-hash-table 'equal?))
(define current-file (make-parameter #f))
(define current-ftable (make-parameter #f))
(define current-rtable (make-parameter #f))
(define (table? t) (list? t))

(define (list-files)
  (hash-table-map tables
		  (lambda (k v)
		    (list k (if (table? v)
				(ref v 2)
				v)))))

(define (switch-file name)
  (let1 table (ref tables name #f)
    (cond
     ((table? table)
      (current-file name)
      (current-ftable (ref table 0))
      (current-rtable (ref table 1))
      (list #t))
     (else
      (error "Not loaded yet: " name)))))
	

(define (load-file file)
  (call-with-input-process `(callp ,file)
    (lambda (input)
      (receive (ftable rtable so)
	  (load-entries (read-line input))
	(set! (ref tables (sys-basename file))
	      (list ftable rtable file))
	(switch-file (sys-basename file))
	(for-each
	 (lambda (s) 
	   (unless (ref tables s #f)
		   (set! (ref tables s) 'UNSOLVED)))
	 so))
      (list))))

(define (load-entries file)
  (define (acceptable? entry)
    (not (any (lambda (r) (r entry)) blacklist)))
  (define (wash! es)
    (let1 connections (fold-right 
		       (lambda (entry0 kdr)
			 (let1 entry (ref entry0 2)
			   (if (acceptable? entry)
			       (cons entry0 kdr)
			       kdr)))
		       (list)
		       (delete-duplicates 
			(cadr (memq :connections es))))
      (set! (cadr (memq :connections es)) connections)))
  (with-input-from-file file
    (lambda ()
      (let loop ((es (read))
		 (ftable (make-hash-table 'equal?))
		 (rtable (make-hash-table 'equal?))
		 (so (list)))
	  (if (eof-object? es)
	      (values ftable rtable so)
	      (cond
	       ((and (eq? (car es) 'objdump-dcall)
		     (eq? (cadr es) 'entry))
		(wash! es)
		  (for-each
		   (lambda (entry)
		     (hash-table-push! rtable entry es))
		   (delete-duplicates
		    (map
		     (cute ref <> 2)
		     (cadr (memq :connections es)))))
		  (when (acceptable? (ref es 3))
			(hash-table-push! ftable (ref es 3) es))
		  (loop (read) ftable rtable so))
	       ((eq? (car es) 'readelf-so)
		(loop (read) ftable rtable (append (cdr es) so)))
	       (else
		(loop (read) ftable rtable so))))))))

(define (find-callers sym table)
  (define (build kar l)
    `(,(if kar 
	   kar
	   (if (cadr (memq :variable? l))
	       'v
	       'f))
      ,(ref l 3)
      :file ,(cadr (memq :file l)) 
      :line ,(cadr (memq :line l))
      ))
  (cond
   ((ref table sym #f) => (cute map (cute build #f <>) <>))
   (else
    (hash-table-fold 
     table 
     (lambda (k v kdr)
       (if (and (string-prefix? sym k)
		(memq (string-ref k (string-length sym))
		      '(#\@ #\.)))
	   (append (map (cute build 'x <>) v) kdr)
	   kdr))
     (list)))))

(define (find-callees sym table)
  (if-let1 ess (ref table sym #f)
	   (append-map
	    (lambda (es)
	      (let1 connections (cadr (memq :connections es))
		(map (lambda (name)
		       (if-let1 callee-ess (ref table name #f)
				(append-map
				 (lambda (callee-es)
				   `(,(if (cadr (memq :variable? callee-es))
					  'v
					  'f) 
				     ,(ref callee-es 3)
				     :file ,(cadr (memq :file callee-es))
				     :line ,(cadr (memq :line callee-es))
				     #;:caller #;(,(ref es 2) 
					      :file ,(cadr (memq :file es))
					      :line ,(cadr (memq :line es))))
				   )
				 callee-ess)
				`(x ,name)))
		     (map (cute ref <> 2) connections))))
	    ess)
	   (list)))

(define (writeln es)
  (write es)
  (newline)
  ;(print es)
  )
(define (writeln* ess)
  (for-each
   writeln
   ess))

(define (forward-reachable? from to ftable)
  (list #f))
(define (backward-reachable? from to ftable)
  (list #f))

(define (decorate name table interactive?)
  (if interactive?
      #`",(if (variable? name table) '= '|| ),|name|,(if (variable? name table) '|| '() )"
      name))

(define (callers* name rtable interactive? depth)
  (hash-table-map
   (let loop ((current-callers (map
				(cute ref <> 1)
				(find-callers name rtable)))
	      (total-callers (make-hash-table 'equal?))
	      (path (list name))
	      (depth depth))
     (when (or (and (boolean? depth) depth)
	       (and (integer? depth) (< 0 depth)))
       (for-each
	(lambda (caller)
	  (unless (member caller path)
	    (let* ((xpath (cons caller path))
		   (v (hash-table-get total-callers caller #f)))
	      (unless (and v (member xpath v)) ; necessary
                (hash-table-push! total-callers caller 
				  xpath)
		(loop (map
		       (cute ref <> 1)
		       (find-callers caller rtable))
		      total-callers
		      xpath
		      (if (integer? depth) (- depth 1) depth)
		    )))))
	current-callers))
     total-callers)
   (lambda (a b)
     (cons (decorate a rtable interactive?)
	   (map
	    (lambda (x)
	      (map (cute decorate <> rtable interactive?) x))
	    b))
     )))

(define (callees* name ftable interactive? depth)
  (hash-table-map
   (let loop ((current-callees (map
				(cute ref <> 1)
				(find-callees name ftable)))
	      (total-callees (make-hash-table 'equal?))
	      (path (list name))
	      (depth depth))
     (when (or (and (boolean? depth) depth)
	       (and (integer? depth) (< 0 depth)))
       (for-each
	(lambda (callee)
	  (unless (member callee path)
	    (let* ((xpath (cons callee path))
		   (v (hash-table-get total-callees callee #f)))
	      (unless (and v (member xpath v)) 
		(hash-table-push! total-callees callee 
				  xpath)
		(loop (map
		       (cute ref <> 1)
		       (find-callees callee ftable))
		      total-callees
		      xpath
		      (if (integer? depth) (- depth 1) depth))))))
	current-callees))
     total-callees)
   (lambda (a b)
     (cons (decorate a ftable interactive?) 
	   (map
	    (lambda (x)
	      (map (cute decorate <> ftable interactive?)
		   x))
	    (map reverse b))))))

(define (reachable? from to rtable interactive? depth)
  (delete-duplicates 
   (or (let1 callers-from (callers* from rtable #f depth)
	     (if-let1 found (assoc-ref  callers-from to #f)
		      (map
		       (lambda (f)
			 (map (cute decorate <> rtable interactive?)
			      f))
		       found)
		      #f))
       (let1 callers-to (callers* to rtable #f depth)
	     (if-let1 found (assoc-ref  callers-to from #f)
		      (map
		       (lambda (f)
			 (map (cute decorate <> rtable interactive?)
			      f))
		       found)
		      #f))
       (list #f))))

(define (transit from to ftable rtable interactive? depth)
  (define (decorate* tree)
    (map (lambda (t) (map (cute decorate <> rtable interactive?) t)) 
	 tree))
  (delete-duplicates
   (let1 r (reachable? from to rtable #f depth)
	 (if (equal? r (list #f))
	     (let1 commons (append (let* ((callers-from (callers* from rtable #f depth))
					  (callers-to (callers* to rtable #f depth))
					  (common-callers (lset-intersection equal? 
									     (map car callers-from)
									     (map car callers-to))))
				     (if (null? common-callers)
					 (list)
					 (map
					  (lambda (common-caller)
					    `(common-caller ,common-caller
							    :from ,(decorate*
								    (delete-duplicates 
								     (assoc-ref callers-from common-caller)))
							    :to ,(decorate*
								  (delete-duplicates
								   (assoc-ref callers-to common-caller))))
					    )
					  common-callers)))
				   (let* ((callees-from (callees* from ftable #f depth))
					  (callees-to (callees* to ftable #f depth))
					  (common-callees (lset-intersection equal? 
									     (map car callees-from)
									     (map car callees-to))))
				     (if (null? common-callees)
					 (list)
					 (map
					  (lambda (common-callee)
					    `(common-callee ,common-callee
							    :from ,(decorate*
								    (delete-duplicates
								     (assoc-ref callees-from common-callee)))
							    :to ,(decorate* 
								  (delete-duplicates
								   (assoc-ref callees-to common-callee))))
					    )
					  common-callees))))
		   (if (null? commons)
		       (list #f)
		       commons))
	     r))))

(define (variable? name table)
  (let1 entry (ref table name #f)
    (if entry
	(cadr (memq :variable? (car entry)))
	#f)))

(define (desc name ftable)
  (ref ftable name (list #f)))

(define (fline0 table file line)
  (let1 m (#/^\/srv\/debugs(\/sources\/[a-zA-Z0-9]\/[^\/]+\/[^\/]+).*/
			   (caddr (ref tables (current-file))))
    `(,#`"/srv/sources,(m 1)/pre-build/,|file|" ,#`",|line|")))

(define (fline name ftable)
  (or (and-let* ((e (ref ftable name (list #f)))
		 (e (car e))
		 (e (cdddr e))
		 (file (kref e :file))
		 (line (kref e :line)))
	(list (fline0 ftable file line)))
      (list #f)))
(define (fline-call caller callee ftable)
  (or (and-let* ((e (ref ftable caller (list #f)))
		 (e (car e))
		 (conn (kref e :connections))
		 (ee (any (lambda (ee) 
			     (let1 ee-name (ref ee 2)
			       (if (or (equal? ee-name callee)
				       (equal? (car (string-split ee-name #/@|\./))
					       (car (string-split callee #/@|\./))))
				   ee
				   #f)))
			   conn))
		 (e (cdddr e))
		 (file (kref e :file))
		 (line (or (kref ee :line)
			   (kref e :line)
			   ))
		 (rel (car ee)))
	;; rel
	(list (fline0 ftable file line)))
      (list)))

(define (preview generator viewer)
  (list 
   (if-let1 f (let1 r (generator) (if (null? r) #f (car r)))
	    (if (null? f)
		#f
		(viewer  (car f) (cadr f)))
	    #f)))
(define (firefox0 file line)
  (let ((b (regexp-replace #/^\/srv\/sources/ file "http://sources/ysh"))
	(l line))
    (run-process `(firefox ,(format "~a#L:~a" b l)))))
(define (emacs0 file line)
  (run-process `(emacsclient --no-wait ,#`"+,line" ,file)))
(define (inline0 file line)
  (define (print/prefix prefix l)
    (display prefix)
    (print l))
  (let* ((range 3)
	 (lines (file->string-list file))
	 (l (- (string->number line) range))
	 (l (if (< l 0) 0 l)))
    (for-each-with-index
     (lambda (index line)
       (print/prefix #`";,(+ l index):	" line))
     (take (drop lines l) range))
    (list)))

(define (inline name ftable)
  (preview (pa$ fline name ftable) inline0))
(define (inline-call caller callee ftable)
  (preview (pa$ fline-call caller callee ftable) inline0))
(define (emacs name ftable)
  (preview (pa$ fline name ftable) emacs0))
(define (emacs-call caller callee ftable)
  (preview (pa$ fline-call caller callee ftable) emacs0))
(define (web name ftable)
  (preview (pa$ fline name ftable) firefox0))
(define (web-call caller callee ftable)
  (preview (pa$ fline-call caller callee ftable) firefox0))


(define (search name ftable)
  (hash-table-fold ftable (lambda (k v kdr)
			    (if (string-scan k name)
				(cons k kdr)
				kdr))
		   (list)))

(define (path->dists path)
  (rxmatch-if (#/^\/srv\/debugs(\/sources\/[a-zA-Z0-9]\/[^\/]+)(\/[^\/]+).*/ path)
      (#f head rv)
    (fold
     (lambda (entry kdr)
       (let1 r (sys-readlink entry)
	 (if (equal? r #`".,|rv|")
	     (cons ((#/^\^alias-(.*)/ (sys-basename entry)) 1) kdr)
	     kdr)))
     (list)
     (glob #`"/srv/sources,|head|/^alias-*"))
    #f))

(define (list-loadable name)
  ;; c/p/vr
  (glob-fold "/srv/debugs/sources/*/*/*/plugins/O0g3/root"
	     (lambda (entry kdr)
	       (append 
		;; Handle goache's bug. May be fixed in F15.
		(directory-fold entry
				(lambda (path seed)
				  (if (equal? (sys-basename path) name)
				      (cons (list path 
						  (map string->symbol (path->dists path))
						  ) seed)
				      seed))
				(list)
				)
		kdr))
	       (list)))

(define (entry-points ftable interactive?)
  (map
   (cute decorate <> ftable interactive?)
   (hash-table-fold ftable (lambda (k v kdr)
			     (if (any (cute kref <> :entry-point) v)
				 (cons k kdr)
				 kdr))
		    (list))))


(define (source file)
  (for-each
   (lambda (es)
     (display ";; context: ")
     (write (current-file))
     (newline)
     (display ";; expression: ")
     (write es)
     (newline)
     (writeln* (callq-eval es #f))
     (newline)
     )
   (file->sexp-list file))
  (list))

(define handlers (list))

(define-macro (defcmd name pattern action)
  `(set! handlers (cons
		   (list ',name :pattern ',pattern
			 :handler (match-lambda 
				      (,pattern ,action)
				    (else 'next)))
		   handlers)))

;; (handler es)
;; => next
;; => (...)
(define (callq-eval es env)
  (let loop ((handlers handlers))
    (if (null? handlers)
	(begin (print #`";; unknown command: ,(car es)")
	       (list #f))
	(let1 r ((kref (car handlers) :handler) es)
	  (if (eq? r 'next)
	      (loop (cdr handlers))
	      r)))))

(define (help cmd)
  (define (help-all)
    (map 
     (lambda (h) 
       `(,(car h) => ,(kref h :pattern))) 
     handlers))
  (if cmd
      (filter (lambda (e) (eq? (car e) cmd)) (help-all))
      (help-all)))

(defcmd help ('help) (help #f))
(defcmd help ('help (? symbol? s)) (help s))
(defcmd source ('source (? string? file)) (source file))
(defcmd load ('load (? string? file)) (load-file file))
(defcmd switch ('switch (? string? file)) (switch-file file))
(defcmd files ('files) (list-files))
(defcmd candidate ('candidate (? string? name)) (list-loadable name))

(defcmd desc ('desc (? string? name)) (desc name (current-ftable)))
(defcmd desc ('? (? string? name)) (desc name (current-ftable)))

(defcmd fline ('fline (? string? name)) (fline name (current-ftable)))
(defcmd fline (': (? string? name)) (fline name (current-ftable)))
(defcmd fline 
  ('fline (? string? caller) (? string? callee)) 
  (fline-call caller 
	      callee
	      (current-ftable)))
(defcmd fline 
  (': (? string? caller) (? string? callee)) 
  (fline-call caller 
	      callee
	      (current-ftable)))


(defcmd inline ('inline (? string? name)) (inline name (current-ftable)))
(defcmd inline ('l (? string? name)) (inline name (current-ftable)))
(defcmd inline
  ('inline (? string? caller) (? string? callee)) 
  (inline-call caller callee (current-ftable)))
(defcmd inline
  ('l (? string? caller) (? string? callee))
  (inline-call caller callee (current-ftable)))

(defcmd emacs ('emacs (? string? name)) (emacs name (current-ftable)))
(defcmd emacs ('j (? string? name)) (emacs name (current-ftable)))
(defcmd emacs 
  ('emacs (? string? caller) (? string? callee))
  (emacs-call caller callee (current-ftable)))
(defcmd emacs
  ('j (? string? caller) (? string? callee))
  (emacs-call caller callee (current-ftable)))

(defcmd web ('web (? string? name)) (web name (current-ftable)))
(defcmd web ('u (? string? name)) (web name (current-ftable)))
(defcmd web 
  ('web (? string? caller) (? string? callee)) 
  (web caller callee (current-ftable)))
(defcmd web 
  ('u (? string? caller) (? string? callee)) 
  (web caller callee (current-ftable)))

(defcmd search ('search (? string? name)) (search name (current-ftable)))
(defcmd search ('* (? string? name)) (search name (current-ftable)))

(defcmd dump ('dump) (rtable-dump (current-rtable)))


(defcmd callers ('< (? string? name)) (callers* name (current-rtable) #t 1))
(defcmd callers ('callers (? string? name)) (callers* name (current-rtable) #t 1))
(defcmd callers ('< (? string? name) depth) (callers* name (current-rtable) #t depth))
(defcmd callers ('callers (? string? name) depth) (callers* name (current-rtable) #t depth))

(defcmd callees ('> (? string? name)) (callees* name (current-ftable) #t 1))
(defcmd callees ('callees (? string? name)) (callees* name (current-ftable) #t 1))
(defcmd callees ('> (? string? name) depth) (callees* name (current-ftable) #t depth))
(defcmd callees ('callees (? string? name) depth) (callees* name (current-ftable) #t depth))


(defcmd reachable? ('reachable? from to) (reachable? from to (current-rtable) #t 1))
(defcmd reachable? ('reachable? from to  depth) (reachable? from to (current-rtable) #t depth))
(defcmd reachable? ('<> from to) (reachable? from to (current-rtable) #t 1))
(defcmd reachable? ('<> from to  depth) (reachable? from to (current-rtable) #t depth))

(defcmd transit ('transit from to) (transit from to (current-ftable) (current-rtable) #t #t))
(defcmd transit ('transit from to  depth) (transit from to (current-ftable) (current-rtable) #t depth))
(defcmd transit ('<*> from to) (transit from to (current-ftable) (current-rtable) #t #t))
(defcmd transit ('<*> from to  depth) (transit from to (current-ftable) (current-rtable) #t depth))

(defcmd entry-points ('entry-points) (entry-points (current-ftable) #t))
(defcmd entry-points ('$) (entry-points (current-ftable) #t))


(define (rtable-dump table)
  (apply append (hash-table-map table (lambda (k v)
					(map
					 (lambda (v0) 
					   `(=> ,k ,(ref v0 3)))
					 (delete-duplicates v))
					))))

;; timeout, plugin output routine, plugin input
(define (print-usage name status)
  (print "Usage: ")
  (print #`"	,|name| -h|--help")
  (print #`"	,|name| [--batch] [-s|--script|--source SCRIPT]... [-l|--load BINARY]...")
  (exit status))

(define (main args)
  (let-args (cdr args)
      ((help   "h|help" => (pa$ print-usage (car args) 0))
       (script "s|script|source=s" => (pa$ source))
       (load   "l|load=s" => (pa$ load-file))
       (batch  "b|batch")
       . rest)
    (when batch
      (exit 0))
    (for-each load-file rest)
    (read-eval-print-loop read
			  (cute callq-eval <> <>)
			  writeln*
			  (lambda () 
			    (with-output-to-port (current-error-port)
			      (lambda ()
				(display (if batch "" #`"[,(current-file)]? "))
				(flush)))))
    (newline)))
